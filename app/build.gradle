apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def commitCount = Integer.parseInt('git rev-list --all --count'.execute([], project.rootDir).text.trim())

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lucasurbas.masterdetail"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode commitCount
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString(),
                             "room.incremental":"true"
                ]
                compilerArgumentProvider new EnableDaggerIncrementalAP()
            }
        }
    }


    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding.enabled = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Support dependencies
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.core:core-ktx:1.2.0-alpha04'
    implementation 'androidx.activity:activity-ktx:1.1.0-alpha03'
    implementation 'androidx.fragment:fragment-ktx:1.2.0-alpha03'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta04'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'


    // Architecture components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha04'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha04'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0-alpha04"

    // Dependencies for Android Architecture Components: Room SQLite database framework
    implementation "androidx.room:room-runtime:2.2.0-rc01"
    kapt "androidx.room:room-compiler:2.2.0-rc01"
    implementation "androidx.sqlite:sqlite-ktx:2.0.1"
    // optional - Coroutines support for Room
    implementation "androidx.room:room-ktx:2.2.0-rc01"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.1"

    // Dagger dependencies
    kapt "com.google.dagger:dagger-compiler:2.23.2"
    implementation "com.google.dagger:dagger:2.23.2"

    // Butterknife dependencies
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'

    testImplementation 'junit:junit:4.12'
}

class EnableDaggerIncrementalAP implements CommandLineArgumentProvider {

    @Override
    Iterable<String> asArguments() {
        return ["-Adagger.gradle.incremental"]
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def commitCount = Integer.parseInt('git rev-list --all --count'.execute([], project.rootDir).text.trim())

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.lucasurbas.masterdetail"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode commitCount
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString(),
                             "room.incremental":"true"
                ]
            }
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support dependencies
    implementation 'com.google.android.material:material:1.3.0-rc01'
    implementation 'androidx.core:core-ktx:1.5.0-beta01'
    implementation 'androidx.activity:activity-ktx:1.2.0-rc01'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-rc01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'


    // Architecture components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-rc01'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-rc01"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.0-rc01"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-rc01"

    // Dependencies for Android Architecture Components: Room SQLite database framework
    implementation "androidx.room:room-runtime:2.2.6"
    kapt "androidx.room:room-compiler:2.2.6"
    implementation "androidx.sqlite:sqlite-ktx:2.1.0"
    // optional - Coroutines support for Room
    implementation "androidx.room:room-ktx:2.2.6"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

    // Dagger dependencies
    kapt "com.google.dagger:dagger-compiler:2.31.1"
    implementation "com.google.dagger:dagger:2.31.1"

    testImplementation 'junit:junit:4.13.1'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:1.3.1-alpha03"
    androidTestImplementation "androidx.test:rules:1.3.1-alpha03"
    androidTestImplementation "androidx.test:monitor:1.3.1-alpha03"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3-alpha03"
    androidTestImplementation "androidx.test.ext:truth:1.3.1-alpha03"
    androidTestImplementation "androidx.test:core-ktx:1.3.1-alpha03"
    androidTestUtil "androidx.test:orchestrator:1.3.1-alpha03"

    //Dependencies for Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0-alpha03"

    // Espresso intents for checking launched intents
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0-alpha03"

    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0-alpha03"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def commitCount = Integer.parseInt('git rev-list --all --count'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.lucasurbas.masterdetail"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode commitCount
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en"
        javaCompileOptions {
            annotationProcessorOptions {
                compilerArgumentProvider new EnableDaggerIncrementalAP()
            }
        }
    }

    dataBinding.enabled = true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Support dependencies
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    // Architecture components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha01'
    kapt 'androidx.room:room-compiler:2.1.0-alpha01'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.0.0'

    // Dagger dependencies
    kapt "com.google.dagger:dagger-compiler:2.18"
    implementation "com.google.dagger:dagger:2.18"

    // Butterknife dependencies
    implementation 'com.jakewharton:butterknife:9.0.0-rc1'
    kapt 'com.jakewharton:butterknife-compiler:9.0.0-rc1'

    testImplementation 'junit:junit:4.12'

    kapt 'com.android.databinding:compiler:3.3.0-beta01'
}

class EnableDaggerIncrementalAP implements CommandLineArgumentProvider {

    @Override
    Iterable<String> asArguments() {
        return ["-Adagger.gradle.incremental"]
    }
}